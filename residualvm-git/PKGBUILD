# Maintainer: Joel Teichroeb <joel@teichroeb.net>

pkgname=residualvm-git
pkgver=r4616.79a7ab1
pkgrel=1
pkgdesc="A cross-platform 3D game interpreter which allows you to play LucasArts' LUA-based 3D adventures"
arch=('i686' 'x86_64')
url='http://www.residualvm.org'
license=(GPL)
depends=('sdl' 'zlib' 'alsa-lib' 'glu' 'libmad' 'mesa' 'libmpeg2' 'freetype2')
makedepends=('git')
source=(git://github.com/residualvm/residualvm.git)
sha1sums=('SKIP')

pkgver() {
	cd residualvm
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd residualvm
	./configure --prefix=/usr
	make
}

package() {
	cd residualvm
	make DESTDIR=${pkgdir} install
}
