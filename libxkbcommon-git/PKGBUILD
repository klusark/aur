# Maintainer: Joel Teichroeb <joel@teichroeb.net>

pkgname=libxkbcommon-git
pkgver=0.4.0.r78.gcaf8f3b
pkgrel=1
pkgdesc="A library to handle keyboard descriptions."
url="http://xkbcommon.org/"
arch=(i686 x86_64)
license=('custom')
depends=('glibc')
makedepends=('git' 'xorg-util-macros' 'xkeyboard-config' 'libxcb')
provides=('libxkbcommon')
replaces=('libxkbcommon')
conflicts=('libxkbcommon')
source=(git://github.com/xkbcommon/libxkbcommon.git)
sha1sums=('SKIP')

pkgver() {
	cd libxkbcommon
	git describe --long | sed -r 's/^xkbcommon.//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd libxkbcommon

	./autogen.sh --prefix=/usr
	make
}

package() {
	cd libxkbcommon

	make DESTDIR=${pkgdir} install
}
