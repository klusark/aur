# Maintainer: Joel Teichroeb <joel@teichroeb.net>
# Contributor: Sébastien Luttringer
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgname=weston-git
pkgver=1.4.0.r96.g97b5220
pkgrel=1
pkgdesc='Reference implementation of a Wayland compositor'
arch=(i686 x86_64)
url='http://wayland.freedesktop.org'
license=('MIT')
makedepends=('git')
depends=('libxkbcommon' 'wayland-git' 'mesa' 'poppler-glib' 'mtdev' 'libva'
         'libxcursor' 'glu' 'cairo' 'pixman' 'libunwind' 'pango' 'colord' 'libwebp')
conflicts=('weston')
provides=('weston')

source=('git://anongit.freedesktop.org/wayland/weston')
sha1sums=('SKIP')

pkgver() {
	cd weston
	git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd weston
	./autogen.sh --prefix=/usr \
		--libexecdir=/usr/lib/weston \
		--enable-demo-clients-install \
		--with-cairo=gl
	make
}

package() {
	cd weston
	make DESTDIR="${pkgdir}" install

	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

	install -Dm644 "shared/zalloc.h" "$pkgdir/usr/include/weston/zalloc.h"
	install -Dm644 "config.h" "$pkgdir/usr/include/weston/config.h"
}

